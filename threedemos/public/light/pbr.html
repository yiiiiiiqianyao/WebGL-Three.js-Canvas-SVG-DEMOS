<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="UTF-8" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0" />
    <meta http-equiv="X-UA-Compatible" content="ie=edge" />
    <link rel="shortcut icon" href="../favicon.ico" />
    <link rel="bookmark" href="../favicon.ico" />
    <title>直接光照的 PBR</title>
    <style>
      body {
        margin: 0;
        overflow: hidden;
      }
    </style>
    <script src="../src/three.js"></script>
    <script src="../src/controller/OrbitControls.js"></script>
    <script src="../src/libs/stats.min.js"></script>
    <script type="x-shader/x-vertex" id="vertexShader">
      varying vec3 v_normal;
      varying vec3 v_worldPos;
      void main() {
          v_normal = normal;
          vec4 worldPos = modelMatrix * vec4(position, 1.0);
          v_worldPos = worldPos.xyz;
          gl_Position = projectionMatrix * viewMatrix * worldPos;
      }</script>
    <script type="x-shader/x-vertex" id="fragmentShader">
      #define PI 3.141592653589793

      uniform vec3 color;
      uniform vec3 u_eye;
      varying vec3 v_normal;
      varying vec3 v_worldPos;


      // cosTheta 为入射角
      // F0 为入射角度接近 0(入射方向靠近法向量)时的 Fresnel 反射系数
      vec3 fresnelSchlick(float cosTheta, vec3 F0) {
        return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
      }

      // 正态分布函数 D （NDF）GGXTR
      // roughness 粗糙度
      float DistributionGGX(vec3 N, vec3 H, float roughness) {
          // 用粗糙度的平方代替粗糙度，是根据迪士尼公司给出的观察以及后来被Epic Games公司采用的光照模型，光照在正太分布函数中采用粗糙度的平方会让光照看起来更加自然
          float a      = roughness * roughness;
          float a2     = a * a;
          float NdotH  = max(dot(N, H), 0.0);
          float NdotH2 = NdotH * NdotH;
      
          float nom   = a2;
          float denom = (NdotH2 * (a2 - 1.0) + 1.0);
          denom = PI * denom * denom;
      
          return nom / denom;
      }
      
      float GeometrySchlickGGX(float NdotV, float roughness) {
          float r = (roughness + 1.0);
          float k = (r*r) / 8.0;
      
          float nom   = NdotV;
          float denom = NdotV * (1.0 - k) + k;
      
          return nom / denom;
      }

      // 衰减系数 G
      float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness) {
          float NdotV = max(dot(N, V), 0.0);
          float NdotL = max(dot(N, L), 0.0);
          float ggx2  = GeometrySchlickGGX(NdotV, roughness);
          float ggx1  = GeometrySchlickGGX(NdotL, roughness);
      
          return ggx1 * ggx2;
      }
      
      void main() {
        vec3 lightColor = vec3(5.0, 1.0, 1.0);
        vec3 lightPosition = vec3(5.0, 5.0, 5.0);
        vec3 worldPos = v_worldPos;
        vec3 eyePosition = u_eye;

        float roughness = 0.15;     // roughness 粗糙度
        float metallic = 0.0;       // metallic 金属度 0代表非金属，1代表金属。尽量使得值靠近0和1

        // 在游戏开发中
        // Albedo 指的是从一个表面反射出能量与入射能量的比例，它的值在0-1之间
        // Diffuse（Coefficient、Diffuse Coefficient） 指的是漫反射出去的能量占入射能量的比例，它的值也在0-1之间
             

        vec3 N = normalize(v_normal);                 // 表面法线
        vec3 L = normalize(lightPosition - worldPos); // 入射光线
        //vec3 V = normalize(eyePosition - worldPos);   // 观察视线
        vec3 V = normalize(max(eyePosition - worldPos, 0.0));   // 观察视线
        vec3 H = normalize(L + V);                    // 半角向量

        // 菲涅尔反射率
        vec3 F0 = vec3(0.04); // 通用绝缘体系数 普通玻璃的反射系数大约是 0.04
        vec3 F = fresnelSchlick(max(dot(H, V), 0.0), F0);
        // 微平面分布系数 正态分布函数（法线分布函数 normal distribution function）
        float NDF = DistributionGGX(N, H, roughness);
        // 衰减系数
        float G = GeometrySmith(N, V, L, roughness);

        vec3 Ks = F; // 反射部分
        vec3 Kd = 1.0 - Ks - metallic; // 折射部分（漫反射）

        float NdotL = max(dot(N, L), 0.0); 

        // ****** Irradiance 部分 ******
        vec3 intensity    = lightColor * 10.0;// 光源输入
        float cosTheta    = NdotL; // 入射角
        float distance    = length(lightPosition - worldPos); // 距离光源的
        float attenuation = 1.0 / pow(distance, 1.0); // 衰减
        vec3 radiance     = intensity * attenuation * cosTheta;
        
        // ****** BRDF 反射部分 ******
        // 一般来说反射率(albedo)纹理在美术人员创建的时候就已经在sRGB空间了，因此我们需要在光照计算之前先把他们转换到线性空间
        // vec3 albedo     = pow(texture(albedoMap, TexCoords).rgb, 2.2);
        vec3 albedo = vec3(1.0, 1.0, 0.0);   // 物体表面颜色 c
        vec3 nominator    = NDF * G * F; // 分子
        float denominator = 4.0 * max(dot(N, V), 0.0) * NdotL + 0.001;  //分母中加了0.001防止除0
        vec3 BRDF         = Kd * albedo / PI + nominator / denominator;
        
        // ****** 合并环境光 ******
        vec3 Lo = BRDF * radiance * NdotL; // 我们并没有把乘进反射方程中，在 BRDF 中已经乘了 F, Ks 等于F, 因此不需要再乘一次
        vec3 ao = vec3(0.4); // ao 从环境光遮蔽纹理中采样得到
        vec3 ambient = vec3(1.0, 1.0, 1.0) * albedo * ao;

        vec3 fcolor = ambient + Lo;
        fcolor = fcolor / (fcolor + vec3(1.0)); // Reinhard ToneMapping 色调映射
        fcolor = pow(fcolor, vec3(1.0/2.2));  // 伽马矫正，将颜色分布转化为非线形

        gl_FragColor = vec4(fcolor, 1.0);
      }
    </script>
  </head>
  <body></body>

 <!-- https://codepen.io/alphardex/pen/OJmPpeJ -->
  <script type="module">
    var scene, camera, renderer, clock, controller, stats
    var shader_material, rayMarchingFireMaterial, shaderMaterial
   

    init();
    animate();

    // - Functions -
    function init() {
      scene = new THREE.Scene();
      clock = new THREE.Clock();
      camera = new THREE.PerspectiveCamera(
        45,
        window.innerWidth / window.innerHeight,
        0.1,
        1000
      );
      camera.position.set(0, 0, 20)
      renderer = new THREE.WebGLRenderer({
        antialias: true, // 开启抗锯齿处理
        alpha: true,
      });
      renderer.setClearColor(0xbfd1e5) // set sky color
      renderer.setSize(window.innerWidth, window.innerHeight);
      renderer.setPixelRatio(window.devicePixelRatio)


      var axisHelper = new THREE.AxesHelper(10);
      scene.add(axisHelper);
     
      var directionalLight = new THREE.DirectionalLight(0xffffff, 0.9);
      directionalLight.position.set(400, 200, 300);
      // directionalLight.castShadow = true
      scene.add(directionalLight);
      // 方向光2
      var directionalLight2 = new THREE.DirectionalLight(0xffffff, 0.9);
      directionalLight2.position.set(-400, -200, -300);
      scene.add(directionalLight2);
      //环境光
      var ambient = new THREE.AmbientLight(0x404040, 0.6);
      scene.add(ambient);

      stats = new Stats()
      document.body.appendChild( stats.dom );

      // --------
      var geometry = new THREE.SphereBufferGeometry(3, 32, 32);
      var material = new THREE.MeshBasicMaterial( {color: 0xff0000} );
      var shader_material = new THREE.ShaderMaterial( {
        uniforms: {
          color: { value: new THREE.Color(1, 1, 0) },
          u_eye: {
            type: 'v3',
            value: camera.position
          }
        },

        vertexShader: document.getElementById( 'vertexShader' ).textContent,
        fragmentShader: document.getElementById( 'fragmentShader' ).textContent,
        // vertexColors: THREE.VertexColors,   // 以顶点颜色为准进行渲染
        // side:THREE.DoubleSide,              // 双面可见
      } );
      var cube = new THREE.Mesh( geometry, shader_material );
      scene.add( cube );
      // --------

      controller = new THREE.OrbitControls(camera, renderer.domElement);
      document.body.appendChild(renderer.domElement);
      window.onresize = onResize;
      console.log(shader_material, camera.position)
    }

    function animate() {
      requestAnimationFrame(animate);
      renderer.render(scene, camera);
      stats.update();
      if(shader_material) {
        shader_material.uniforms.u_eye.value = camera.position;
      }
      // shader_material.uniforms.u_eye.value = camera.position;
      controller.update(clock.getDelta());
    }

    function onResize() {
      camera.aspect = window.innerWidth / window.innerHeight;
      camera.updateProjectionMatrix();
      renderer.setSize(window.innerWidth, window.innerHeight);
    }
  </script>
</html>
